import model.Room
import utils.Game

import state.Combat
import state.Init
import state.PartyAction

import model.PlayerAction

import model.db.Avatar

import utils.Action

import javafx.stage.Stage
import gui.RoomScreen;

dialect  "mvel"

rule "Stirge Room Stealth Check Failed"
    salience 100
    when
        Avatar( getAction() == Action.STEALTH, !rollSuccessful )
        $room: Room( no == 1, cleared == false, isPartyEntering() )
    then
        $room.addToDescription( "\n However, they do attack you." );
        $room.clearActions();
        $room.addPartyAction( new PlayerAction( "Get ready for combat!", Action.COMBAT ) );
        $room.setPartyEntering( false );
        System.out.println( "Stealth Check failed" );
        update( $room )
end

rule "Stirge Room Stealth Check Succeeded"
    salience 100
    when
        $a: Avatar( getAction() == Action.STEALTH, rollSuccessful )
        $room: Room( no == 1, cleared == false, isPartyEntering() )
    then
        System.out.println( "Party entering" );
        $room.clearActions();
        $room.addPartyAction( new PlayerAction( "Sneak towards the next room", Action.STEALTH, 9, true ) );
        $room.addPartyAction( new PlayerAction( "Move towards the next room", Action.COMBAT ) );
        $a.setAction( null );
end

rule "Stirge Room Stealth Action Failed"
    salience 90
    when
        $p: PartyAction()
        $a: Avatar( getAction() == Action.STEALTH, !rollSuccessful )
        $room: Room( no == 1, cleared == false, !isPartyEntering() )
        $stage: Stage()
    then
        $room.addToDescription( "\nThe bats have noticed " + $a.toString() + " and are attacking your group!" );
        $room.clearActions();
        $room.addPartyAction( new PlayerAction( "Get ready for combat!", Action.COMBAT ) );
        new RoomScreen( $stage ).setStage();
        $a.setAction( null );
        retract( $stage )
        retract( $p )
        retract( $room )
end

rule "Stirge Room Stealth Action Succeeded"
    salience 90
    when
        $p: PartyAction()
        $a: Avatar( getAction() == Action.STEALTH, rollSuccessful )
        $room: Room( no == 1, cleared == false, !isPartyEntering() )
        $stage: Stage()
    then
        $a.setAction( null );
        $room.clearActions();
        $room.addPartyAction( new PlayerAction( "Enter next room", Action.CONTINUE, 2 ) );
        $room.addPartyAction( new PlayerAction( "Attack the bats", Action.COMBAT, true ) );
        new RoomScreen( $stage ).setStage();
        $a.setAction( null );
        retract( $stage )
        retract( $p )
        retract( $room )
end

rule "Goblin Room Stealth Action Failed"
    when
        $p: PartyAction()
        $a: Avatar( getAction() == Action.STEALTH, !rollSuccessful )
        $room: Room( no == 2, cleared == false, !isPartyEntering() )
        $stage: Stage()
    then
        System.out.println( "Goblin Sneak failed" );
        $room.addToDescription( "\nThe goblins saw " + $a.toString() + " sneaking behind them and do not seem too happy about it!" );
        $room.clearActions();
        $room.addPartyAction( new PlayerAction( "CHARGE!!!", Action.COMBAT ) );
        new RoomScreen( $stage ).setStage();
        $a.setAction( null );
        retract( $stage )
        retract( $p )
        retract( $room )
end

rule "Goblin Room Stealth Action Succeeded"
    when
        $p: PartyAction()
        $a: Avatar( getAction() == Action.STEALTH, rollSuccessful )
        $room: Room( no == 2, cleared == false, !isPartyEntering() )
        $stage: Stage()
    then
        System.out.println( "Goblin Sneak successful" );
        $a.setAction( null );
        $room.addToDescription( "\nThe goblins didn't notice you." );
        $room.clearActions();
        $room.addPartyAction( new PlayerAction( "Surprise them!", Action.COMBAT, true ) );
        Combat.setPartyAdvantage( true );
        new RoomScreen( $stage ).setStage();
        $a.setAction( null );
        retract( $stage )
        retract( $p )
        retract( $room )
end

rule "Revival Succeeded"
    salience 50
    when
        $a: PlayerAction( getAction() == Action.REVIVE )
        $avatar: Avatar( getAction() == Action.REVIVE, isRollSuccessful() )
        $room: Room() from Game.getCurrentRoom()
        $stage: Stage()
    then
        System.out.println( "Revival Succeeded" );
        $room.addToDescription( "\n" + $avatar.getName() + " successfully revived " + $a.getTarget().getName() );
        $avatar.setAction( null );
        $a.getTarget().setCurrentHitPoints( $a.getTarget().getHitPoints() );
        Game.removePlayerActionFromPartyMembers( $a );
        new RoomScreen( $stage ).setStage();
        retract( $room )
        retract( $a );
        retract( $stage )
end

rule "Revival Failed"
    salience 50
    when
        $a: PlayerAction( getAction() == Action.REVIVE )
        $avatar: Avatar( getAction() == Action.REVIVE, !isRollSuccessful() )
        $room: Room() from Game.getCurrentRoom()
        $stage: Stage()
    then
        System.out.println( "Revival failed" );
        Game.removeFromParty( $a.getTarget() );
        $avatar.setAction( null );
        $room.addToDescription( "\n" + $avatar + " tries to revive " + $a.getTarget() + "..." );
        $room.addToDescription( "\n" + $a.getTarget() + " died!" );
        Game.removePlayerActionFromPartyMembers( $a );
        new RoomScreen( $stage ).setStage();
        retract( $room )
        retract( $a.getTarget() )
        retract( $a );
        retract( $stage )
end

rule "Goblin Room Perception Check Succeeded"
    salience 100
    when
        $a: Avatar( getAction() == Action.PERCEPTION, rollSuccessful )
        $room: Room( no == 2, cleared == false, isPartyEntering() )
    then
        $room.addToDescription( "\n Two Goblins are talking in their native language" );
        $room.setPartyEntering( false );
        System.out.println( "Perception Check (Room 2) Succeeded" );
end

rule "Goblin Room Perception understand" extends "Goblin Room Perception Check Succeeded"
    when
        eval( $a.speaksLanguageOfAMonsterInRoom( $room ) )
    then
        $room.addToDescription( "\n " + $a + " explains, that they are talking about their useless friend Jerry." );
        System.out.println( "Goblins understood" );
        update( $room )
end

rule "Statue Room Perception Check Succeeded"
    salience 100
    when
        $a: Avatar( getAction() == Action.PERCEPTION, rollSuccessful )
        $room: Room( no == 3, !cleared, !isPartyEntering() )
    then
        System.out.println( "Perception on statue successful (" + $a + ")" );
        $room.addToDescription( "\n\n" + $a + " discovers a hidden compartment in this statue that holds a scrap of paper.\n" +
                                "The paper contains the following note:" );
        $room.addToDescription( "\n\n QUIT WASTING TIME! PICK A DOOR AND GO! FLIP A COIN IF YOU HAVE TO—JUST PICK A DIRECTION!\n" +
                                "IT’S NOT ROCKET SCIENCE! WHAT ARE YOU GOING TO DO, SPEND THE WHOLE NIGHT STANDING IN HERE WITH A STATUE?\n" +
                                 "YOU NEED TO PICK A DOOR AND GOOOO! THIS IS, LIKE, THE THIRD F***ING ROOM, WE DON’T HAVE ALL DAY!" );
        $room.setCleared( true );
        $room.removePlayerAction( new PlayerAction( "Further inspect the statue", Action.PERCEPTION, 12, true ) );
end

rule "Statue Room Perception Check Failed"
    salience 100
    when
        $a: Avatar( getAction() == Action.PERCEPTION, !rollSuccessful )
        $room: Room( no == 3, !cleared, !isPartyEntering() )
    then
        System.out.println( "Perception on statue failed (" + $a + ")" );
        $room.addToDescription( "\n\n It is a statue of a woman in armor!" );
        $room.setCleared( true );
        $room.removePlayerAction( new PlayerAction( "Further inspect the statue", Action.PERCEPTION, 12, true ) );
end
